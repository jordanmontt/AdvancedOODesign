{
    "title":"Using well asString and printString",
    "subtitle":"a Pharo code idiom",
    "author":"S. Ducasse"
}

${slide:title=Goal}$

- Think about ""intermediary object creation""
- How to avoid creating spurious objects
- ==asString== and ==printString== inside ==printOn:==


${slide:title=printString: setting the stage}$

[[[
Object >> printString
	^ self printStringLimitedTo: 50000
	
Object >> printStringLimitedTo: limit
	^self printStringLimitedTo: limit using: [:s | self printOn: s]
]]]

${slide:title=printStringLimitedTo:using:}$

==streamContents: [ :s | self printOn: s ]== creates a stream and gather object textual representation

[[[
Object >> printStringLimitedTo: limit
   | limitedString |
   limitedString := String
                                        streamContents: [ :s | self printOn: s ]
                                        limitedTo: limit.
   limitedString size < limit ifTrue: [ ^ limitedString ].
   ^ limitedString , '...etc...'
]]]






${slide:title=The case of displayStringOn:}$

- When we get a stream, better use it directly
[[[
MessageTally >> displayStringOn: aStream
	self displayIdentifierOn: aStream.
	aStream 
		nextPutAll: ' (';
		nextPutAll: self tally printString;
		nextPutAll: ')'
]]]

==self tally printString==
- Creates a new stream
- Get its contents to be able to put in the first stream

${slide:title=Better}$

[[[
MessageTally >> displayStringOn: aStream
	self displayIdentifierOn: aStream.
	aStream 
		nextPutAll: ' (';
		print: self tally;
		nextPutAll: ')'
]]]

[[[
Stream >> print: anObject
	"Have anObject print itself on the receiver."

	anObject printOn: self
]]]

- No creation of intermediary streams

${slide:title=Another case of misuse}$

[[[
printProtocol: protocol sourceCode: sourceCode

	^ String streamContents: [ :stream |
		stream
			nextPutAll: '"protocol: '; 
			nextPutAll: protocol printString;
			nextPut: $"; cr; cr;
			nextPutAll: sourceCode ]	
]]]

==protocol printString==
- Creates a new stream
- Get its contents to be able to put in the first stream

${slide:title=Better use print:}$

[[[
printProtocol: protocol sourceCode: sourceCode

	^ String streamContents: [ :stream |
		stream
			nextPutAll: '"protocol: '; 
			print: protocol;
			nextPut: $"; cr; cr;
			nextPutAll: sourceCode ]	
]]]





${slide:title=About asString}$

==asString== has the similar issues than ==printString==

[[[
Object >> asString
	"Answer a string that represents the receiver."

	^ self printString
]]]

- ==asString== should be used when we convert an object to its string representation
- now check before calling it inside a ==streamContents==


${slide:title=Conclusion}$

- Check protocols (==printString==, ==printOn:==, ==asString==)
- Read code around
- Streams are powerful containers

