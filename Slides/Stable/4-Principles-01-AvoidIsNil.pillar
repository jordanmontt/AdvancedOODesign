{
  "title":"Avoid Null Checks",
  "author" : "S. Ducasse, G. Polito, P. Tesone and L. Fabresse"
}

${slide:title=Goal}$

- Understanding the implication behind returning nil
- Looking at provider side
- Object initialization avoids nil propagation
- Looking at client side
- Null Object 

${slide:title=nil?}$

- Unique instance of the class ==UndefinedObject==
- In Pharo, a real object, as anybody else
- Default value of uninitialized instance variables
- Still we should be careful when to use it

${slide:title=Looking at provider side}$

- What is the impact of code generating nil?


${slide:title=Example}$

Imagine an inferencer that looks for rules that correspond to a fact. 

[[[
| inf | 
inf := Inferencer new.
inf 
	addRule: #sunny -> #'sunglasses';
	addRule: #sunny -> #'solar cream';
	addRule: #rainy -> #'umbrella'.

inf rulesForFact: #sunny 
> #(Fact (sunglasses) Fact(umbrella))

inf rulesForFact: #cloudy
> nil
]]]


${slide:title=Example code}$


[[[
Inferencer >> rulesForFact: aFact
	self noRule ifTrue: [ ^ nil ]
	^ self rulesAppliedTo: aFact
]]]

- Here ==rulesForFact:== returns ==nil== to indicate that there is no rules for a fact.
- What are the consequences?


${slide:title=Consequences!}$

- Returning ==nil== (e.g., ==ifTrue: [ ^ nil ]==) forces ""EVERY"" client to check for ==nil==:


[[[
(inferencer rulesForFact: 'a')
   ifNotNil: [ :rules |
      rules do: [ :each | ... ]
]]]

- Code ends up full of nil checks


${slide:title=Solution: Return polymorphic objects}$

When possible, return polymorphic objects:
- when returning a collection, return an empty one
- when returning a number, return 0


${slide:title=Solution: Return polymorphic objects}$

[[[
Inferencer >> rulesForFact: aFact
	self noRule ifTrue: [ ^ #() ]
	^ self rulesAppliedTo: aFact
]]]

Your clients can just iterate and manipulate the returned value

[[[
(inferencer rulesForFact: 'a') do: [ :each | ... ]
]]]




${slide:title=About nil}$

 Limit the propagation of ==nil==
- by having method returning ==nil==
- avoid storing ==nil==

${slide:title=Initialize your object state}$

Avoid ==nil== checks by initializing your variables:
- By default instance variables are initialized with ==nil==
- The responsibility of an object is to ""correctly initialize"" its state

[[[
Archive >> initialize
	super initialize.
	members := OrderedCollection new
]]]



${slide:title=Sometimes you have to check...}$

- Sometimes you have to check some conditions before doing an action
- When possible, you can turn the default case into an object, a Null Object.

${slide:title=Example}$

[[[
ToolPalette >> nextAction
   self selectedTool
      ifNotNil: [ :tool | tool attachHandles ]

ToolPalette >> previousAction
   self selectedTool
      ifNotNil: [ :tool | tool detachHandles ]
]]]

Here we are forced to check that there is a selected tool. 
- Why not having always one selected?
- Even one doing nothing?

${slide:title=Example}$

+>figures/NullObject2.pdf+


${slide:title=Solution: Use NullObject}$

- A null object proposes a ""polymorphic"" API and embeds default actions/values.
- Woolf, Bobby (1998). "Null Object". In Pattern Languages of Program Design 3. Addison-Wesley.

Let us create a ==NoTool== class whose behavior is to do nothing.


${slide:title=Solution: NoTool}$

[[[
AbstractTool << #NoTool
]]]

[[[
NoTool >> attachHandles
   ^ self
   
NoTool >> detachHandles
   ^ self
]]]

${slide:title=Solution: Use NullObject}$

Initialize the ==ToolPalette== with a ==NoTool== instance.

[[[
ToolPalette >> initialize
   self selectedTool: NoTool new
]]]

And we get no forced ==ifNil:== tests anymore

[[[   
ToolPalette >> nextAction
   self selectedTool attachHandles
   
ToolPalette >> previousAction
   self selectedTool detachHandles
]]]

${slide:title=Solution: With initialization and NoTool}$

+>figures/NullObject3.pdf+


${slide:title=Difficulty with NullObject}$

Sometimes it is difficult to apply the ==NullObject==
- Too large API
- Or would need too many NullObjects
- Unclear default "no behavior"

${slide:title=For exceptional cases, use exceptions}$

For exceptional cases, replace ==nil== by exceptions:
- ""avoid"" error codes because they require ==if== in clients
- exceptions may be handled by the client, or the client's client, or ...

[[[
FileStream >> nextPutAll: aByteArray
   canWrite ifFalse: [ self cantWriteError ].
   ...
FileStream >> cantWriteError
   (CantWriteError file: file) signal
]]]


${slide:title=Conclusion}$

- A message acts as a better ==if==
- Avoid null checks, return ""polymorphic"" objects instead
- Initialize your variables
- If you can, create objects representing ""default behavior""
